/**
* Name: Errors
* Author: Alexis Drogoul
* Description: A model that demonstrates basic features of errors / errors handling in GAML
* Tags: GAML
*/
model Errors

/**
 * In this experiment, an error is raised by the model itself and should interrupt the flow of execution
 */
experiment "Raise an error"
{
	init
	{
		error "This error is generated by the model itself";
	}

}
/**
 * In this experiment, an error is raised by the model itself but it is caught and displayed in the console without being reported and stopping the execution
 */
experiment "Raise and catch an error"
{
	init
	{
		try {
			error "This error is generated by the model itself";
		} catch {
			write # current_error + " but is not reported";
		}

	}
 
}


/**
* In this experiment, an error is provoked by making a division by zero but it is caught and displayed in the console without being reported and stopping the execution
 */
experiment "Provoke and catch an error"
{
	init
	{
		try {
			float error <- 1 / 0;
		} catch {
			write "A " + # current_error + " is caught";
		}

	}

}

/**
* In this experiment, an error is provoked by making a division by zero but the use of 'try' makes it silent (i.e. no 'catch' block) 
 */
experiment "Provoke a silent error"
{
	init
	{
		try {
			float error <- 1 / 0;
		} 
	}
}